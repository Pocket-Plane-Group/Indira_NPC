BACKUP ~IndiNPC/Backup~
//AUTHOR ~ghreyfain@pocketplane.net~
SUPPORT ~https://forums.pocketplane.net/index.php?topic=16098.0~
VERSION ~v14~
README ~IndiNPC/Docs/indi_readme.html~

AUTO_TRA ~IndiNPC/Tra/autotra/%s~

ALWAYS
  INCLUDE ~IndiNPC/Lib/er_lib.tpa~
  LAF autotra_workaround END
  LAM bgee_language

// Adds CD_STATE_NOTVALID state
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
UNLESS ~CD_STATE_NOTVALID~

END

LANGUAGE ~English~
         ~English~
         ~IndiNPC/Tra/English/game.tra~
         ~IndiNPC/Tra/English/setup.tra~
LANGUAGE ~Italiano~
         ~Italian~
         ~IndiNPC/Tra/English/game.tra~
         ~IndiNPC/Tra/English/setup.tra~
         ~IndiNPC/Tra/Italian/game.tra~
         ~IndiNPC/Tra/Italian/setup.tra~
LANGUAGE ~Francais~
         ~French~
         ~IndiNPC/Tra/English/game.tra~
         ~IndiNPC/Tra/English/setup.tra~
         ~IndiNPC/Tra/French/game.tra~
         ~IndiNPC/Tra/French/setup.tra~
LANGUAGE ~Polski~
         ~Polish~
         ~IndiNPC/Tra/English/game.tra~
         ~IndiNPC/Tra/English/setup.tra~
         ~IndiNPC/Tra/Polish/game.tra~
         ~IndiNPC/Tra/Polish/setup.tra~
LANGUAGE ~Espanol~
         ~Spanish~
         ~IndiNPC/Tra/English/game.tra~
         ~IndiNPC/Tra/English/setup.tra~
         ~IndiNPC/Tra/Spanish/game.tra~
         ~IndiNPC/Tra/Spanish/setup.tra~
LANGUAGE ~Deutsch~
         ~German~
         ~IndiNPC/Tra/English/game.tra~
         ~IndiNPC/Tra/English/setup.tra~
         ~IndiNPC/Tra/German/game.tra~
         ~IndiNPC/Tra/German/setup.tra~
LANGUAGE ~Russian~
         ~Russian~
         ~IndiNPC/Tra/English/game.tra~
         ~IndiNPC/Tra/English/setup.tra~
         ~IndiNPC/Tra/Russian/game.tra~

BEGIN @0

REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgee bgt eet~ @2000

COPY ~IndiNPC/Creatures/J#Indi01.cre~ ~override/J#Indi01.cre~
  WRITE_SHORT 0x28 0x6111
  SAY NAME1 @1
  SAY NAME2 @1
  SAY INITIAL_MEETING @2 
  SAY MORALE @3 
  SAY HAPPY @4 
  SAY UNHAPPY_ANNOYED @5 
  SAY UNHAPPY_SERIOUS @6 
  SAY UNHAPPY_BREAKING @7 
  SAY LEADER @8 
  SAY TIRED @9 
  SAY BORED @10 
  SAY BATTLE_CRY1 @11 
  SAY DAMAGE ~~ [J#indi11]
  SAY DYING ~~ [J#indi12]
  SAY HURT @14 
  SAY AREA_FOREST @15 
  SAY AREA_DUNGEON @16 
  SAY AREA_CITY @17 
  SAY AREA_DAY @18 
  SAY AREA_NIGHT @19 
  SAY SELECT_COMMON1 @20 
  SAY SELECT_COMMON2 @21 
  SAY SELECT_COMMON3 @22 
  SAY SELECT_ACTION1 @23 
  SAY SELECT_ACTION2 @24 
  SAY SELECT_ACTION3 @25 
  SAY BIO @26
  LAUNCH_PATCH_MACRO ~ER_PATCH_ITEMS_ON_INDIRA~
/* This stuff was originally so the thing could work with BG1.  Converted to Tutu, so it's unnecessary.
ADD_GAME_NPC ~J#Indi01~ ~AR5201~ #1012 #1467 */

COPY ~IndiNPC/Creatures/J#Indi03.cre~ ~override/J#Indi03.cre~
     ~IndiNPC/Creatures/J#Indi04.cre~ ~override/J#Indi04.cre~
  WRITE_SHORT 0x28 0x6111
  SAY NAME1 @1
  SAY NAME2 @1
  SAY INITIAL_MEETING @2 
  SAY MORALE @3 
  SAY HAPPY @4 
  SAY UNHAPPY_ANNOYED @5 
  SAY UNHAPPY_SERIOUS @6 
  SAY UNHAPPY_BREAKING @7 
  SAY LEADER @8 
  SAY TIRED @9 
  SAY BORED @10 
  SAY BATTLE_CRY1 @11 
  SAY DAMAGE ~~ [J#indi11]
  SAY DYING ~~ [J#indi12]
  SAY HURT @14 
  SAY AREA_FOREST @15 
  SAY AREA_DUNGEON @16 
  SAY AREA_CITY @17 
  SAY AREA_DAY @18 
  SAY AREA_NIGHT @19 
  SAY SELECT_COMMON1 @20 
  SAY SELECT_COMMON2 @21 
  SAY SELECT_COMMON3 @22 
  SAY SELECT_ACTION1 @23 
  SAY SELECT_ACTION2 @24 
  SAY SELECT_ACTION3 @25 
  SAY BIO @26
  LAUNCH_PATCH_MACRO ~ER_PATCH_ITEMS_ON_INDIRA~

ACTION_IF NOT FILE_EXISTS_IN_GAME ~%er_bimoen%.dlg~ BEGIN
	COMPILE EVALUATE_BUFFER ~IndiNPC/bimoen.d~
END

ACTION_IF GAME_IS ~eet~ BEGIN
	APPEND ~BGBANTER.2DA~ ~%er_uimoen%        %er_bimoen%~ UNLESS ~%er_uimoen%~
END ELSE BEGIN
	APPEND ~Interdia.2da~ ~%er_uimoen%        %er_bimoen%~ UNLESS ~%er_uimoen%~
END

COPY_EXISTING ~%er_FirewineRuins%.are~ ~override/%er_FirewineRuins%.are~
	WRITE_EVALUATED_ASCII 0x94 ~%er_5201%~
	BUT_ONLY_IF_IT_CHANGES


COMPILE EVALUATE_BUFFER ~IndiNPC/Dialogue/indi.d~
  USING ~%er_tra_path%/English/indi.tra~
        ~%er_tra_path%/%LANGUAGE%/indi.tra~

//Mur'Neth-Indira banter
ACTION_IF FILE_EXISTS ~override/BA#MURN.DLG~ AND NOT FILE_EXISTS ~override/INDIRAMURNETH.XXX~
THEN BEGIN
COPY_EXISTING ~SW1H01.ITM~ ~override/INDIRAMURNETH.XXX~
COMPILE EVALUATE_BUFFER ~IndiNPC/Dialogue/murnethbanter.d~
END

COPY ~IndiNPC/Graphics~ ~override~
COPY ~IndiNPC/Portraits/%er_port%~ ~override~
COPY ~IndiNPC/Items/J#indi01.itm~ ~override/J#indi01.itm~
  SAY NAME1 @27
  SAY NAME2 @27
  SAY DESC @28

ACTION_IF NOT FILE_EXISTS_IN_GAME ~J#indi.bcs~ BEGIN
<<<<<<<<J#indi.baf
>>>>>>>>
	COMPILE ~J#indi.baf~
END

//Finch-Indira banter
ACTION_IF FILE_EXISTS ~override/bsufinch.DLG~ AND NOT FILE_EXISTS ~override/INDIRAFINCH.XXX~
THEN BEGIN
  COPY_EXISTING ~SW1H01.ITM~ ~override/INDIRAFINCH.XXX~
  COMPILE EVALUATE_BUFFER ~IndiNPC/Dialogue/xmodfinchindi.d~
  EXTEND_TOP ~J#indi.bcs~ ~IndiNPC/Scripts/append_j#indi.baf~ EVALUATE_BUFFER
END

EXTEND_TOP ~%er_5201%.bcs~ ~IndiNPC/Scripts/_ar5201.baf~

// SoD/BGT compatibility: Indira leaves the party at transition.
ACTION_IF (GAME_IS ~bgee~ AND GAME_INCLUDES ~sod~) OR (GAME_IS ~eet~) THEN BEGIN
	EXTEND_TOP ~bd0103.bcs~ ~IndiNPC/Scripts/append_bd0103.baf~
END 
ACTION_IF GAME_IS ~bgt~ THEN BEGIN
	EXTEND_TOP ~aram00.bcs~ ~IndiNPC/Scripts/append_aram00.baf~
END

ACTION_IF GAME_IS ~eet~ BEGIN
	APPEND ~BGDIALOG.2da~ ~J#Indi       J#IndiP            J#IndiJ          J#IndiD~ UNLESS ~J#Indi~
	APPEND ~BGBANTER.2da~ ~J#Indi       J#IndiB~                                     UNLESS ~J#Indi~
END ELSE BEGIN
	APPEND ~PDialog.2da~  ~J#Indi       J#IndiP            J#IndiJ          J#IndiD~ UNLESS ~J#Indi~
	APPEND ~Interdia.2da~ ~J#Indi       J#IndiB~                                     UNLESS ~J#Indi~
END


LAF HANDLE_AUDIO END


ACTION_IF GAME_IS ~eet~ BEGIN
	ACTION_MATCH ~%LANGUAGE%~ WITH
		~English~  BEGIN OUTER_SPRINT lng ~en_US~ END
		~Francais~ BEGIN OUTER_SPRINT lng ~fr_FR~ END
		~Polski~   BEGIN OUTER_SPRINT lng ~pl_PL~ END
		~Espanol~  BEGIN OUTER_SPRINT lng ~es_ES~ END
		~Deutsch~  BEGIN OUTER_SPRINT lng ~de_DE~ END
		~Russian~  BEGIN OUTER_SPRINT lng ~ru_RU~ END
	DEFAULT
		OUTER_SPRINT lng ~en_US~
	END
	INCLUDE ~EET/other/EET_functions.tph~
	LAF ~EET_NPC_TRANSITION~
		INT_VAR
			type = 1
		STR_VAR
			dv = "J#Indi"
			override_BG1 = "J#Indi"
			traFile = EVAL "EET/lang/%lng%/compatibility.tra"
			string = "@5000000"
	END
END

BEGIN @2001 /* ~Install Interjections for Indira (BETA)~ */
REQUIRE_COMPONENT ~setup-indinpc.tp2~ 0 @2002 /* ~Indira NPC mod main component needs to be installed.~ */

COMPILE EVALUATE_BUFFER ~IndiNPC/Dialogue/indij.d~
